// === LIBRARY ===
#include <WiFi.h>
#include <Wire.h>
#include <Adafruit_VL53L0X.h>
#include <NewPing.h>
#include <Firebase_ESP_Client.h>
#include "pitches.h"

const char* ssid     = "ssid";
const char* password = "password";

#define DATABASE_URL "https://firebase-default-rtdb.firebaseio.com/"
#define DATABASE_SECRET "secret"

const uint8_t POMPA1_PIN = 23;
const uint8_t POMPA2_PIN = 32;
const uint8_t POMPA3_PIN = 33;
const uint8_t POMPA4_PIN = 26;
const uint8_t MOTOR_PIN  = 27;
const uint8_t SYSTEM_PIN = 14;

const uint8_t BUZZER_PIN  = 13;
const uint8_t BUZZER2_PIN = 12;

// HC-SR04 utama
const uint8_t TRIG_PIN = 18;
const uint8_t ECHO_PIN = 19;

// HC-SR04 tangki
const uint8_t TRIG2_PIN = 17;
const uint8_t ECHO2_PIN = 5;

const unsigned int MAX_DISTANCE_CM = 400;

const uint8_t SDA_PIN = 21;
const uint8_t SCL_PIN = 22;

Adafruit_VL53L0X lox = Adafruit_VL53L0X();
NewPing sonar(TRIG_PIN, ECHO_PIN, MAX_DISTANCE_CM);
NewPing sonarTank(TRIG2_PIN, ECHO2_PIN, MAX_DISTANCE_CM);

bool loxReady = false;

const uint16_t TOF_EMPTY_MM  = 100;
const uint16_t ULTRASONIC_THRESHOLD_CM = 40;
const uint16_t TANK_FULL_THRESHOLD_CM = 10;

uint16_t tof_mm   = 65535;
uint16_t us_cm    = 65535;
uint16_t tank_cm  = 65535;

unsigned long lastSend = 0;
const unsigned long sendInterval = 3000;

FirebaseData fbdo;
FirebaseAuth auth;
FirebaseConfig config;

const int tetris_melody[] = {
  NOTE_E5, NOTE_B4, NOTE_C5, NOTE_D5,
  NOTE_C5, NOTE_B4, NOTE_A4,
  NOTE_A4, NOTE_C5, NOTE_E5, NOTE_D5,
  NOTE_C5, NOTE_B4, NOTE_C5, NOTE_D5,
  NOTE_E5, NOTE_C5, NOTE_A4, NOTE_A4
};
const int tetris_durations[] = {
  150,150,150,150,
  150,150,300,
  150,150,150,150,
  150,300,150,150,
  150,150,150,300
};

const int tetris_len = sizeof(tetris_melody) / sizeof(tetris_melody[0]);

bool tetrisActive = false;
unsigned long tetrisLastChange = 0;
int tetrisIndex = 0;

void playTetris() {
  if (!tetrisActive) return;

  if (millis() - tetrisLastChange >= tetris_durations[tetrisIndex]) {
    int f = tetris_melody[tetrisIndex];

    if (f > 0) tone(BUZZER2_PIN, f);
    else noTone(BUZZER2_PIN);

    tetrisLastChange = millis();
    tetrisIndex++;

    if (tetrisIndex >= tetris_len) tetrisIndex = 0;  // LOOP
  }
}

int readOptoButton(uint8_t pin) {
  return (digitalRead(pin) == LOW) ? 1 : 0;
}

int readDevice(uint8_t pin, bool inverted = false) {
  int val = readOptoButton(pin);
  return inverted ? !val : val;
}

void readSensors() {
  if (loxReady) {
    VL53L0X_RangingMeasurementData_t measure;
    lox.rangingTest(&measure, false);
    tof_mm = (measure.RangeStatus != 4) ? measure.RangeMilliMeter : 65535;
  }

  us_cm = sonar.ping_cm();
  if (us_cm == 0) us_cm = 65535;

  tank_cm = sonarTank.ping_cm();
  if (tank_cm == 0) tank_cm = 65535;
}

void updateBuzzer() {

  bool tofEmpty = (tof_mm == 65535) || (tof_mm > TOF_EMPTY_MM);
  bool usEmpty  = (us_cm  == 65535) || (us_cm  > ULTRASONIC_THRESHOLD_CM);
  bool sensorsEmpty = tofEmpty && usEmpty;

  // ------------ Buzzer 12 = TETRIS (saat sensor kosong) -------
  if (sensorsEmpty) {
    if (!tetrisActive) {
      tetrisActive = true;
      tetrisLastChange = millis();
      tetrisIndex = 0;
    }
  } else {
    tetrisActive = false;
    tetrisIndex = 0;
    noTone(BUZZER2_PIN);
  }

  //Buzzer 13
  // Trigger pakai pin 14
  if (digitalRead(SYSTEM_PIN) == HIGH) {
    digitalWrite(BUZZER_PIN, HIGH);  // Bunyi normal
  } else {
    digitalWrite(BUZZER_PIN, LOW);
  }

  playTetris();
}

void sendToFirebase() {
  if (WiFi.status() != WL_CONNECTED) return;

  int pompa1 = readDevice(POMPA1_PIN, true);
  int pompa2 = readDevice(POMPA2_PIN, true);
  int pompa3 = readDevice(POMPA3_PIN, true);
  int pompa4 = readDevice(POMPA4_PIN, true);
  int motor  = readDevice(MOTOR_PIN,  true);
  int system = readDevice(SYSTEM_PIN, true);

  int tof_status   = (tof_mm  != 65535 && tof_mm  <= TOF_EMPTY_MM)            ? 1 : 0;
  int us_status    = (us_cm   != 65535 && us_cm   <= ULTRASONIC_THRESHOLD_CM) ? 1 : 0;
  int tank_status  = (tank_cm != 65535 && tank_cm <= TANK_FULL_THRESHOLD_CM)  ? 1 : 0;

  FirebaseJson json;
  json.set("pompa1", pompa1);
  json.set("pompa2", pompa2);
  json.set("pompa3", pompa3);
  json.set("pompa4", pompa4);
  json.set("motor", motor);
  json.set("system", system);
  json.set("tof", tof_status);
  json.set("us", us_status);
  json.set("tank", tank_status);

  Firebase.RTDB.setJSON(&fbdo, "/data", &json);
}

void setup() {
  Serial.begin(115200);

  pinMode(POMPA1_PIN, INPUT_PULLUP);
  pinMode(POMPA2_PIN, INPUT_PULLUP);
  pinMode(POMPA3_PIN, INPUT_PULLUP);
  pinMode(POMPA4_PIN, INPUT_PULLUP);
  pinMode(MOTOR_PIN,  INPUT_PULLUP);
  pinMode(SYSTEM_PIN, INPUT_PULLUP);

  pinMode(BUZZER_PIN, OUTPUT);
  pinMode(BUZZER2_PIN, OUTPUT);
  digitalWrite(BUZZER_PIN, LOW);
  digitalWrite(BUZZER2_PIN, LOW);

  Wire.begin(SDA_PIN, SCL_PIN);

  loxReady = lox.begin();
  Serial.println(loxReady ? "VL53L0X ready" : "VL53L0X NOT detected");

  WiFi.begin(ssid, password);
  Serial.print("Connecting WiFi");
  while (WiFi.status() != WL_CONNECTED) {
    delay(300);
    Serial.print(".");
  }
  Serial.println("\nWiFi connected");

  config.database_url = DATABASE_URL;
  config.signer.tokens.legacy_token = DATABASE_SECRET;
  Firebase.begin(&config, &auth);
  Firebase.reconnectWiFi(true);
}

void loop() {
  readSensors();
  updateBuzzer();

  Serial.printf("TOF:%u | US:%u | TANK:%u | TETRIS:%d | BUZZ13:%d\n",
                tof_mm, us_cm, tank_cm,
                tetrisActive ? 1 : 0,
                digitalRead(BUZZER_PIN));

  if (millis() - lastSend >= sendInterval) {
    sendToFirebase();
    lastSend = millis();
  }
}
